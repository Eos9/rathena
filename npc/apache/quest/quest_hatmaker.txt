//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshoponly"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
morocc,199,90,4	script	รวมเควสหมวกในเกม	4_M_EINMAN2,{ callfunc "qshoponly"; }
// prontera,163,173,6	script	Quest Shop#2	998,{ callfunc "qshoponly",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	quest_shoponly	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"รวมเควสหมวกในเกมเท่านั้น";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

// Shop
	Add(1,5086,1,0,0,1095,3000,2288,1);	//Alarm Mask
	Add(1,5074,1,20000,0,2254,1,2286,1);	//Angel Wing Ears
	Add(1,2284,1,0,0,923,20);	//Antlers
	Add(1,5174,1,0,0,7445,7,7446,7,7447,7,7448,7,954,300,975,1,733,5,7166,50);	//Ayam [1]
	Add(1,5110,1,0,0,526,2,7270,1,941,1,10004,1);	//Baby Pacifier
	Add(1,5042,1,0,0,10007,1,968,50);	//Bao Bao
	Add(1,5076,1,0,0,2226,1,7038,500);	//Beanie
	Add(1,2296,1,50000,0,2243,1,999,100);	//Binoculars
	Add(1,5057,1,10000,0,2213,1,983,1,914,200);	//Black Cat Ears
	Add(1,5065,1,0,0,624,1,959,300,551,50,1023,1,938,100,7030,1);	//Blue Fish
	sleep 10;
	Add(1,5052,1,0,0,2211,1,978,1,7003,300);	//Blue Hairband
	Add(1,5040,1,0,0,7047,100);	//Blush
	Add(1,5016,1,0,0,1030,10);	//Boy's Cap
	Add(1,5169,1,0,0,706,1,7201,500,7200,10,1049,20,1059,500,1054,2,1024,100);	//Bride Mask [1]
	Add(1,5034,1,0,0,2233,1,746,20);	//Bulb Band
	Add(1,2214,1,0,0,949,100,706,1,722,1,2213,1);	//Bunny Band
	Add(1,5024,1,0,0,529,10,530,5,539,20,999,10,538,15);	//Cake Hat
	Add(1,5028,1,0,0,2279,1,7035,50,526,100);	//Candle
	Add(1,5175,1,100000,0,7111,100,938,99,983,1);	//Censor Bar
	Add(1,5026,1,0,0,1036,450,949,330,539,120,982,1);	//Chef Hat
	sleep 10;
	Add(1,5075,1,0,0,2248,1,7030,108,7194,108,7120,4);	//Cowboy Hat
	Add(1,5048,1,0,0,5041,1,999,10);	//Cresent Hairpin
	Add(1,5036,1,0,0,2608,1,7069,500);	//Cross Hat
	Add(1,5080,1,20000,0,10006,1,714,1,969,3);	//Crown of Ancient Queen
	Add(1,5081,1,40000,0,2249,1,714,1,969,3);	//Crown of Mistress
	Add(1,5107,1,0,0,519,50,7031,50,548,50,539,50);	//Crunch Toast
	Add(1,5091,1,20000,0,10016,1,714,1,969,3);	//Decorative Golden Bell
	Add(1,5082,1,0,0,921,300);	//Decorative Mushroom
	Add(1,5038,1,0,0,1038,600,7048,40);	//Deviruchi Hat
	Add(1,2273,1,3500,0,2275,1,998,50,733,1);	//Doctor Band
	sleep 10;
	Add(1,5058,1,0,0,2233,1,983,1,7206,300,7030,1);	//Drooping Cat
	Add(1,2283,1,5000,0,724,1,5001,1,949,200);	//Ear Muffs
	Add(1,5068,1,20000,0,2255,1,2286,1);	//Evil Wing Ears
	Add(1,5047,1,0,0,2271,1,975,1);	//Fashionable Glasses
	Add(1,5170,1,0,0,5172,1,7063,100,982,1);	//Feather Beret
	Add(1,5018,1,500,0,2247,1,916,300);	//Feather Bonnet
	Add(1,5061,1,20000,0,2269,1,999,10);	//Flower Hairpin
	Add(1,5063,1,0,0,970,1,930,500);	//Giant Band Aid
	Add(1,5021,1,0,0,2233,1,969,1,999,20,949,80,938,800);	//Grief for Greed
	Add(1,5176,1,0,0,706,1,7201,500,7200,10,7015,20,952,100,1028,100);	//Hahoe Mask
	sleep 10;
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2);	//Hat of the Sun God
	Add(1,5001,1,0,0,999,40,984,1,970,1,1003,1);	//Headset
	Add(1,5041,1,0,0,7013,1200);	//Heart Hairpin
	Add(1,5025,1,0,0,2229,1,2254,1,7036,5);	//Helm of Angel
	Add(1,5094,1,0,0,968,1,2299,1,1124,1,931,1);	//Helmet of Orc Hero
	Add(1,5070,1,0,0,7216,300,7097,300,2211,1,982,1);	//Hot-blooded Headband
	Add(1,5071,1,10000,0,5010,1,5049,1,7101,10);	//Indian Headband
	Add(1,5069,1,0,0,1022,999);	//Kitsune Mask
	Add(1,5084,1,0,0,1026,1000,7065,100,945,10,7030,1);	//Lazy Smokie
	Add(1,5027,1,0,0,2252,1,1036,400,7001,50,4052,1);	//Mage Hat
	sleep 10;
	Add(1,5045,1,0,0,2252,1,1054,450,943,1200);	//Magician Hat
	Add(1,5173,1,0,0,7445,7,7446,7,7447,7,7448,7,954,300,983,1,733,5,2221,1);	//Magistrate Hat [1]
	Add(1,5031,1,0,0,5009,1,5028,1,747,1,999,25);	//Mine Hat
	Add(1,5073,1,0,0,2285,1,1550,1);	//Model Training Hat
	Add(1,2278,1,0,0,705,10,909,10,914,10);	//Mr. Smile
	Add(1,5117,1,50000,0,731,10,748,3,982,1);	//Mystic Rose
	Add(1,5177,1,0,0,706,1,7201,500,7200,10,1048,500,1053,2,980,1);	//Mythical Lion Mask [1]
	Add(1,2281,1,5000,0,998,20,707,1);	//Opera Masque
	Add(1,5043,1,0,0,2281,1,1048,50);	//Opera Phantom Mask
	Add(1,5004,1,0,0,701,5);	//Oxygen Mask
	sleep 10;
	Add(1,5023,1,0,0,1059,150,907,100,978,1);	//Parcel Hat
	Add(1,5060,1,0,0,2236,1,7151,100,7111,100);	//Party Hat
	Add(1,5012,1,0,0,710,1,703,1,704,1,708,1);	//Ph.D Hat
	Add(1,2293,1,0,0,1049,4);	//Pretend Murdered
	Add(1,5033,1,0,0,1036,20,2213,1,7065,300,7012,200);	//Raccoon Hat
	Add(1,5039,1,0,0,7030,50,978,1,5015,1);	//Rainbow Eggshell
	Add(1,5109,1,50000,0,10015,1,10007,1,975,1,5032,1);	//Red Bonnet
	Add(1,5083,1,0,0,2244,1,2209,1,10007,1);	//Red Ribbon
	Add(1,5108,1,1887,0,7301,1887,5120,1,611,10);	//Renown Detective's Cap [1]
	Add(1,2280,1,10000,0,1019,120);	//Sakkat
	sleep 10;
	Add(1,5078,1,0,0,5033,1,5064,1);	//Sea-Otter Hat
	Add(1,5243,1,0,0,1022,20,7038,10,7166,10,7065,20,7217,1);	//Shafka [1]
	Add(1,5064,1,0,0,945,600,7030,1);	//Smokie Leaf
	Add(1,5067,1,0,0,5062,1,952,50,1907,1);	//Sombrero
	Add(1,5029,1,0,0,7068,300,7033,850,1015,1);	//Spore Hat
	Add(1,2272,1,91100,0,1019,50,983,1);	//Stop Post	
	Add(1,5062,1,0,0,2280,1,7197,300,7150,300);	//Straw Hat
	Add(1,5049,1,0,0,1099,1500);	//Striped Hairband
	Add(1,5032,1,0,0,1059,250,2221,1,2227,1,7063,600);	//Sunday Hat
	Add(1,5059,1,0,0,5030,1,7213,100,7217,100,7161,300);	//Teddybear Hat
	sleep 10;
	Add(1,5077,1,0,0,2278,1,975,1);	//Tulip Hairpin
	Add(1,5171,1,10000000,0,4219,1,4114,1,4177,1,4259,1,4212,1,4073,1,4112,1,4081,1,4251,1,4166,1,7511,1000,7563,1000);	//Valkyrie Helm [1]
	Add(1,2292,1,2000,0,999,50);	//Welding Mask
	Add(1,5115,1,50000,0,983,1,7267,999,749,1);	//Winter Hat	
	Add(1,5050,1,0,0,5037,1,7064,500);	//Wonder Nutshell
	Add(1,5079,1,0,0,2294,1,7220,400);	//X Hairpin
	Add(1,5121,1,0,0,7263,1,660,1,7099,30);	//Zealotus Mask

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshoponly"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"มีข้อผิดพลาดเกิดขึ้น.";
		end;
	}
	dispbottom "เลือกได้แค่ครั้งละหนึ่งชิ้น.";
	callshop "qshoponly"+@shop_index,1;
	npcshopattach "qshoponly"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"เจ้าไม่สามารถสร้าง "+getitemname(.@q[0])+"ได้มากกว่านี้.";
		end;
	}
	mes "[เควสหมวก]";
	mes "สิ่งที่ต้องการสร้าง: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "ของที่ใช้สร้าง:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ สร้าง ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ ลองสวมใส่...":"")+": ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[เควสหมวก]";
				mes "เจ้าขาดหรือมีของไม่พอแลก.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[เควสหมวก]";
				mes "^FF0000เจ้าต้องมีพื้นที่น้ำหนัก "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" เป็นอย่างน้อยในการสร้างของครั้งนี้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" ได้สร้าง "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "รางวัลเควส #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "เควสต้องการ #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshoponly"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshoponly	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shoponly::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshoponly1	-1,909:-1
