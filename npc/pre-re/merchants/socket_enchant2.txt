//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Leablem]";
	mes "สวัสดี ข้าชื่อ Leablem";
	mes "และข้าก็เป็นผู้เชี่ยวชาญด้าน";
	mes "การเพิ่ม Solt Item";
	mes "ให้กับอาวุธแลกเครื่องป้องกัน";
	mes "แต่ข้าชอบในสิ่งที่ข้าทำและก็ภูมิใจกับมัน";
	next;
	mes "[Leablem]";
	mes "ข้าได้เรียนรู้มากที่สุดในสิ่งที่ได้จากพี่ชายของฉัน Seyablem";
	mes "แต่ข้าคิดว่าฉันยังคงมีจำนวนมากที่จะเรียนรู้";
	mes "ดังนั้นโปรดเข้าใจด้วยว่าการเจาะรูไอเท็ม";
	mes "นั้นยังมีข้อจำกัดไม่เหมือนกับพี่ชาย Seyablem";
	next;
	mes "[Leablem]";
	mes "พี่ข้าไม่ค่อยชอบใจนักเรื่องสอน";
	mes "ความสามารถของเขาให้ข้าเพราะเขา";
	mes "กังวลว่าเขาจะไม่ใช่คนเดียวที่ทำได้";
	mes "อีกต่อไป แต่อย่างไรก็ตาม";
	mes "เขาก็สอนข้าอย่างเต็มที่";
	next;
	mes "[Leablem]";
	mes "ขอบคุณเขามาที่สอนให้ข้าเรียนรู้หลายอย่าง";
	mes "ข้าไม่ค่อยมั่นใจมากเท่าไหร่";
	mes "ในการทดลองทำหลายๆ อย่าง";
	mes "แต่ข้าก็จะทำเท่าที่ข้าจะทำได้ละกัน";
	next;
	mes "[Leablem]";
	mes "จริงๆ แล้วมีอยู่อย่างหนึ่ง";
	mes "ที่พี่ข้าไม่อยากให้ทำ";
	mes "ข้าไม่อยากให้เขาเสียใจ..ดังนั้นข้ามเรื่องนั้นไปดีกว่า";
	mes "ข้าจะไม่พูดถึงมันอีกต่อไป";
	next;
	mes "[Leablem]";
	mes "ช่างเหอะ แล้วเจ้ามี";
	mes "อะไรมาให้ข้าเจาะรูล่ะ ?";
	mes "ค่าบริการ วัสดุ";
	mes "และ โอกาสสำเร็จนั้น";
	mes "ขึ้นอยู่กับไอเท็มที่จะทำนะ";
	next;
	switch(select("- อาวุธ:- เครื่องป้องกัน:- สิ่งที่เจ้าไม่ยอมพูดถึง:- ข้อมูลอื่นๆ:- ยกเลิก"))
	{
		case 1:
			mes "[Leablem]";
			mes "อาวุธใช่ไหม? ได้เลยไม่มีปัญหา";
			mes "อาวุธแบ่งออกเป็น 4 ประเภท";
			mes "ตามระดับความยากของงาน";
			mes "ประเภท C ง่ายสุดและประเภท S นั้นยากที่สุด";
			mes "เจ้าจะลองประเภทไหนล่ะ ?";
			next;
			switch(select("- ระดับ C:- ระดับ B:- ระดับ A:- ระดับ S"))
			{
				case 1:
					mes "[Leablem]";
					mes "ประเภท C นะ";
					mes "แล้วจะเจาะอาวุธอะไรล่ะ";
					next;
					switch(select("- Book of Mother Earth:- Book of Billows:- Book of Gust of Wind:- Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าเลือกประเภท B นะ";
					mes "แล้วจะเจาะอาวุธอะไรล่ะ";
					next;
					switch(select("- Orcish Axe:- Scimiter:- Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,66,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "ประเภท A ดูเหมือนเจ้าจะอยากได้ของดี แต่ไม่มีปัญหา";
					mes "แล้วจะเอาอาวุธอะไรล่ะ ?";
					next;
					switch(select("- Dragon Killer:- Katar of Quaking:- Katar of Raging Blaze:- Katar of Frozen Icicle:- Katar of Piercing Wind:- Golden Mace:- Oriental Lute:- Queen's Whip:- Spectral Spear:- Gae Bolg:- Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,66,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,66,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,66,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,66,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,66,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,66,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,66,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "หืม ประเภท S เลยหรอ เจ้าพูดจริงป่าว *อาการสั่น*";
					mes "อันไหนที่เจ้าต้องการเจาะรู ?";
					next;
					switch(select("- Zephyrus:- Mailbreaker:- Dragon Slayer:- Swordbreaker:- Assasin Dagger:- Grand Cross:- Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "เครื่องป้องกันหรือ ? ได้เลยไม่มีปัญหา";
			mes "อาวุธจะแบ่งออกเป็น 4 ระดับที่แตกต่างกัน";
			mes "ขึ้นอยู่กับความยากลำบากในการทำงาน";
			mes "ประเภท C ง่ายสุดและประเภท S นั้นยากที่สุด";
			mes "เจ้าจะลองประเภทไหนล่ะ ?";
			next;
			switch(select("- ระดับ C:- ระดับ B:- ระดับ A:- ระดับ S"))
			{
				case 1:
					mes "[Leablem]";
					mes "ประเภท C นะ";
					mes "แล้วจะเจาะอาวุธอะไรล่ะ";
					next;
					switch(select("- Sunflower:- Ph.D Hat:- Big Ribbon:- Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าเลือกประเภท B";
					mes "แล้วจะเจาะอาวุธอะไรล่ะ ?";
					next;
					switch(select("- Skull Ring:- High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "ประเภท A ?";
					mes "ดูเหมือนเจ้าจะอยากได้ของดีแต่ไม่มีปัญหา";
					mes "แล้วจะเจาะอาวุธอะไรล่ะ ?";
					next;
					switch(select("- Pirate Bandana:- Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
					}
				case 4:
					mes "[Leablem]";
					mes "หืม ประเภท S !! พูดจริงป่าว? *อาการสั่น*";
					mes "อันไหนที่เจ้าต้องการเจาะรู !!";
					next;
					switch(select("- Mage Coat:- Holy Robe:- Sacred Mission:- Undershirt:- Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,40,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,40,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "โอ้ มันไม่ได้พิเศษอะไรหรอก";
			mes "แต่พี่ข้าคิดว่ามันไม่ใช่สิ่งที่มนุษย์ควรจะไปหลงมัน";
			mes "ข้าไม่อยากทำให้พี่ข้าเสียใจ";
			mes "ข้าควรลืมมันไปเสียดีกว่า";
			next;
			mes "[Leablem]";
			mes "..โอกาสสำเร็จนั้นมีมากอยู่";
			mes "แต่ใช้วัสดุและเงินเยอะมาก";
			mes "ข้าก็ไม่คิดว่าใครจะบ้าทำ";
			mes "ถึงแม้จะเปิดให้บริการเอาไว้";
			next;
			mes "[Leablem]";
			mes "...อืมมๆ...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "ดูเหมือนเจ้าจะสนใจหรือ";
			mes "ถ้าเจ้ะต้องการข้าบอกเจ้าได้";
			mes "แต่เจ้าต้องสัญญาว่าจะเก็บเป็นความลับ";
			mes "ถ้าพี่ข้ารู้ว่าบอกเจ้าละก็";
			mes "เขาเอาข้าตายแน่";
			next;
			mes "[Leablem]";
			mes "บริการนี้มีโอกาสสำเร็จถึง 90%";
			mes "แต่ต้องใช้ Gold 2ea , เงิน 200,000,000Zeny";
			mes "ข้าสามารถเจาะรู้ให้กับ Hat Of The Sun God";
			next;
			mes "[Leablem]";
			mes "พี่ข้าเคยบอกว่า ตั้งแต่โบราณมีกฏห้ามสิ่งนี้เอาไว้";
			mes "แต่ข้าเชื่อว่ามนุษย์มีความอยากรู้อยากเห็น";
			mes "และชอบทดลอง โดยเฉพาะสิ่งที่ถูกห้าม";
			mes "ข้าก็แค่ไม่อยากให้พี่มาด่าข้าแค่นั้นแหล่ะ";
			next;
			switch(select("ข้าจะทำ","ยกเลิก"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...อะ... อะไรน่ะ? ไม่ๆ!";
					mes "นี่จะให้ข้าเป็นคนไม่ดีหรือ";
					mes "อย่าทำกับข้าแบบนี้เลย!";
					mes "ไม่ ข้าไม่ทำ ไม่คือ ไม่ ไม่โว้ย";
					next;
					mes "[Leablem]";
					mes "ไม่ ข้าจะไม่ ไม่โว้ย";
					next;
					mes "[Leablem]";
					mes "ข้าไม่สามารถทำมันได้";
					next;
					mes "[Leablem]";
					mes "ข้าไม่สามารถทำ...ได้..";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "อืมมม...";
					next;
					mes "[Leablem]";
					mes "เจ้านี้ดื้อจริงๆ";
					mes "ก็ได้ข้าจะยกเว้นให้เจ้าสักคนอย่าบอกใครล่ะ";
					mes "โดยเฉพาะพี่ชายข้า สัญญาได้ไหม?";
					next;
					mes "[Leablem]";
					mes "งั้นก็ น้ำสิ่งเหล่านี้มา";
					mes "^FF00002 Gold, เงิน 200,000,000Zeny ค่าบริการ";
					mes "^FF0000และก็ Hat of the Sun God.";
					mes "^FF0000โอกาสสำเร็จมี 90% เจ้ารู้ใช่ไหม?^000000";
					mes "เวลามันมีค่า";
					mes "รีบๆ ลงมือเลยดีกว่า";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "ข้าจะเริ่มต้นตอนนี้?";
						mes "ภาวนาขอให้ของท่านโชคดี";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "ยอดเยี่ยม! ว้าวข้าคิดว่ามีโอกาสประสบความสำเร็จ 90% เป็นความจริง!";
							mes "ดูช่องที่ถูกเพิ่มเข้ามาในสถานที่ที่เหมาะ";
							mes "ขอแสดงความยินดี.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "ครั้งนี้เป็นครั้งแรกและครั้งสุดท้ายที่ข้าจะทำเพื่อเจ้า";
							mes "ไม่เคยบอกใคร";
							mes "เกี่ยวกับเรื่องนี้. เอาล่ะ?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "วา! โอ้พระเจ้าเราล้มเหลว!";
							mes "พระเจ้าของเราข้าคิดว่าเรามีชนิดที่เลวร้ายที่สุดของโชค ...";
							mes "สิ่งที่เราควรทำอย่างไร? ...นี่คือเหตุผล";
							mes "ข้าไม่ได้ต้องการที่จะทำในสถานที่แรก!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "ไม่ไม่เคยถามข้าจะทำนั้นเป็นสิ่งที่มีความเสี่ยงอีกครั้ง!";
							mes "เฮ้อ~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "อืมมม...";
						next;
						mes "[Leablem]";
						mes "เจ้ามีวัสดุดิบไม่พอ";
						mes "แล้วจะให้ข้าลองอะไร?";
						mes "ข้าจะไม่ทำให้เจ้าอีกแล้ว";
						mes "ถึงแม้เจ้าจะมีวัตถุดิบครบก็ตามเฮ้อ!";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "ไม่เคยบอกใคร";
					mes "เกี่ยวกับสิ่งที่ข้าบอกเจ้า เอาล่ะ?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "อยากรู้รายละเอียดเพิ่มหรือ?";
			mes "อืมม ข้าไม่คิดว่าจะมีอะไร";
			mes "ที่ต้องรู้เป็นพิเศษนะ";
			mes "แต่ข้าแนะนำเจ้าได้";
			next;
			mes "[Leablem]";
			mes "การเจาะรูไอเท็มในแต่ละเมือง";
			mes "โอกาสในการเจาะสำเร็จ";
			mes "ขึ้นอยู่กับระดับของอุปกรณ์";
			mes "ข้าไม่รู้ว่าจริงไหม";
			mes "แต่ข้าเห็นมันแตกต่างกันทุกเมือง";
			next;
			mes "[Leablem]";
			mes "อย่างเช่น ช่องเจาะรูเมืองหนึ่ง";
			mes "มีโอกาสเจาะรูสำเร็จ";
			mes "กับอาวุธที่อัพเกรดสูง";
			mes "ในขณะที่ช่องเจาะรูอีกเมืองหนึ่ง";
			mes "เจาะสำเร็จกับอาวุธที่อัพเกรดต่ำ";
			next;
			mes "[Leablem]";
			mes "เมื่อเจ้าล้มเหลวในการเจาะรูไอเท็ม";
			mes "ไอเท็มชิ้นนั้นก็จะหายไป";
			mes "ข้าก็พยายามเต็มที่นะ";
			mes "แต่งานเจาะรูไอเท็มมันเป็นอะไรที่ซับซ้อน";
			next;
			mes "[Leablem]";
			mes "โอ้.มีสิ่งสำคัญท่เจ้าต้องระวังเอาไว้";
			mes "^FF0000อย่าถือไอเท็มชนิดเดียวกันเกินกว่า1ชิ้น";
			mes "^FF0000ในขณะที่ทำการเจาะรูไอเท็ม";
			mes "^FF0000ไม่งั้นอาวุธที่เจ้าไม่ได้ตั้งใจจะเจาะอาจจะกลายเป็นถูกเจาะรู";
			mes "^FF0000และแย่ยิ่งกว่าหากมันล้มเหลวไอเท็มนั้นก็จะหายไป^000000";
			next;
			mes "[Leablem]";
			mes "ข้าก็หวังว่าข้อมูลพวกนี้จะช่วยเจ้าได้";
			mes "ขอบใจมาก";
			close;
		case 5:
			mes "[Leablem]";
			mes "ไม่เป็นไรแล้ว !";
			close;
	}
}


function	script	Func_Socket2	{

	disable_items;
	mes "[Leablem]";
	if(getarg(7,0) != 0 && getarg(8,0) != 0)
	{
		mes getitemname(getarg(0)) + "... เอาล่ะแล้วเจ้าจะต้องนำมาให้ข้า";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zeny เป็นค่าบริการ";
		mes "^FF0000" + getarg(6) + "ea " + getitemname(getarg(5)) + " and " + getarg(8) + "ea " + getitemname(getarg(7)) + ".";
		mes "^FF0000แน่นอน เจ้าต้องมี " + getitemname(getarg(0)) + ".^000000";
	}
	else
	{
		mes getitemname(getarg(0)) + "? เอาล่ะแล้วเจ้าจะต้องนำมาให้ข้า";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zeny เป็นค่าบริการ และ " + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + " เป็นความต้องการ";
		mes "^FF0000แน่นอนไม่ลืมที่จะนำมาให้ข้า" + getitemname(getarg(0)) + ".";
		mes "^FF0000เจ้าควรจะมีรายการทั้งหมด^000000";
	}
	next;
	mes "[Leablem]";
	mes "เจ้าไม่นำทั้งหมดของพวกเขา?";
	mes "สำหรับข้อมูลของข้าถ้าเข้าล้มเหลวในการเจาะรู";
	mes "เจ้าจะสูญเสียทุกความต้องการรายการเช่นเดียวกับเป้าหมาย " + (getiteminfo(getarg(0),5)&2 ? "weapon" : "armor") + "."; // EQP_HAND_R = 2, มันเป็นอาวุธที่มิฉะนั้นเกราะ
	mes "ยังจำได้ถ้า " + (getiteminfo(getarg(0),5)&2 ? "weapon" : "armor") + " ได้รับการอัพเกรดและได้รับการแทรกเกี่ยวกับการ์ดให้";
	mes "เจ้าจะสูญเสียพวกข้าแม้ว่าเจ้าจะประสบความสำเร็จในการเจาะรู";
	next;
	switch(select("ข้าจะทำ.:ยกเลิก"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "เอาล่ะแล้วให้การทำงานเริ่มต้น!";
				mes "เจ้าควรที่จะอธิษฐานเพื่อให้ได้ผลลัพธ์ที่ประสบความสำเร็จ";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "ที่ดีก็ดูเหมือนว่าจะประสบความสำเร็จ";
					mes "มันก็ดูสวยดีทำ ขอแสดงความยินดี!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "แล้วพบกันใหม่อีกครั้งบัดดี้!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "วา! ... ข้าขอโทษที่เป็นเช่นนั้นก็ล้มเหลว";
					mes "แต่ข้าบริสุทธิ์สมบูรณ์";
					mes "นี้เป็นโชคดีของเจ้าและมันถูกลิขิตโดยพระเจ้าโอเค?";
					mes "ไม่ต้องผิดหวังเช่นนั้น,";
					mes "และพยายามในครั้งต่อไป";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[Leablem]";
					mes "แล้วพบกันใหม่อีกครั้งบัดดี้!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "เจ้าโง่หรืออะไร เจ้าไม่ได้นำรายการทั้งหมดที่จำเป็น!";
				mes "นำพวกข้าไปอย่างรวดเร็ว!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "ไว้เจอกันคราวหน้า.";
			close;
	}
}

prontera,245,169,4	duplicate(SocketEnchant2)	Leablem	86
payon,141,103,3	duplicate(SocketEnchant2)	Leablem#payon	86
moc_ruins,154,86,3	duplicate(SocketEnchant2)	Leablem#moc	86