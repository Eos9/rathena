//===== rAthena Script ======================================= 
//= Gemstone Trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//============================================================ 

payon,60,121,4	script	Gemstones#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "เดี๋ยวๆ น้ำหนักของคุณมีมากเกินไป";
		mes "กลับไปตรวจสอบสัมภาระในร่างกาย";
		mes "เสียก่อนแล้วค่อยกลับมาหาฉัน! -";
		end;
	}
	mes "[Jade]";
	mes "ถ้าคุณนำ";
	mes "Gemstones 2 เม็ด";
	mes "สีเดียวกันมาให้ฉัน";
	mes "ฉันจะทำ Gemstones";
	mes "สีอื่นที่คุณต้องการ";
	next;
	switch(select("~ ^3300FFBlue Gemstones^000000 > ^FF0033Red Gemstones^000000:~ ^FF0033Red Gemstones^000000 > ^FF6600Yellow Gemstones^000000:~ ^FF6600Yellow Gemstones^000000 > ^3300FFBlue Gemstones^000000")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	if (countitem(getarg(0)) < 2) {
		mes "[Jade]";
		mes "ฮ่า...!";
		mes "คุณล้อเล่นใช่มั้ย?";
		mes "ฉันไม่สามารถช่วยคุณได้";
		mes "ถ้าคุณมี Gemstone";
		mes "นำมาให้ฉันน้อยกว่า";
		mes "2 "+getitemname(getarg(0))+"ea";
		end;
	}
	else {
		set .@gems,countitem(getarg(0))/2;
		mes "[Jade]";
		mes "ฉันเชื่อว่าฉันทำได้";
		mes "ถ้าคุณจะแลก " + .@gems + " " + getitemname(getarg(1)) + "s";
		mes "คุณมีอยู่ "+getitemname(getarg(0))+"s";
		mes "ที่คุณมีอยู่";
		mes "คุณต้องกาแลกเท่าไรล่ะ?";
		next;
		switch(select("~ แลกทั้งหมด:~ ระบุจำนวน:~ เปล่า, ไม่มีไร")) {
		case 1:
			delitem getarg(0),.@gems * 2;
			getitem getarg(1),.@gems;
			mes "[Jade]";
			mes "คุณจะรู้สึก";
			mes "อยากกลับมา";
			mes "ทุกเวลา";
			mes "หืม..คุณมองอะไรอยู่น่ะ?";
			mes "มีอะไรบนหน้าของฉัน?";
			end;
		case 2:
			mes "[Jade]";
			mes "เอาเท่าไรล่ะ";
			mes "คุณต้องการเท่าไร?";
			mes "สูงสุดคือ 100 เม็ด";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[Jade]";
					mes "ไม่ใส่อะไรเลย?";
					mes "ฉันแนะนำว่า";
					mes "ใคุณคิดใหม่...";
					end;
				}
				else if (.@input > 100) {
					mes "[Jade]";
					mes "อืม...";
					mes "ฉันคิดว่าคุณาน";
					mes "ใส่ตัวเลขมากกว่า";
					mes "จำนวน 100 เม็ด จำได้มั้ย...?";
					next;
				}
				else {
					break;
				}
			}

			if (.@gems > .@input) {
				delitem getarg(0),.@input * 2;
				getitem getarg(1),.@input;
				mes "[Jade]";
				mes "นี่สำหรับคุณ";
				mes "คุณจะรู้สึกอยากกลับมา";
				mes "หาฉันทุกเวลา";
				mes "คุณมองอะไรอยู่น่ะ?";
				mes "มีอะไรบนหน้าฉันงั้นรึ?";
			}
			end;
		case 3:
			mes "[Jade]";
			mes "โอเค ไม่มีปัญหา";
			mes "แล้วกลับมาใหม่ละกัน";
			end;
		}
	}
OnInit:
	waitingroom "เควส Gemstones", 0;
end;
}
